//===== Hercules Script ======================================
//= Sample Quest Log
//===== By: ==================================================
//= AnnieRuru
//===== Current Version: =====================================
//= 0.2
//===== Compatible With: ===================================== 
//= Hercules 2014-09-18
//===== Description: =========================================
//= Demonstrate Quest Log System
//= 1. just talk to npc A->B->C, demonstrate *checkquest
//= 2. countitem quest, just *checkitem, but note the client side description
//= 3. kill monster quest, demonstrate how to use *checkquest( x, HUNTING )
//= 4. time limit quest, demonstrate how to use *checkquest( x, PLAYTIME  )
//===== Topic ================================================
//= http://herc.ws/board/topic/7218-sample-questlog-script/
//===== Additional Comments: =================================  
//= 
//============================================================

/* hercules\db\quest_db.txt
49900,0,0,0,0,0,0,0,"Acolyte Trouble"
49901,0,0,0,0,0,0,0,"Acolyte Trouble"
49902,0,0,0,0,0,0,0,"Acolyte Trouble"
49903,0,0,0,0,0,0,0,"Acolyte Trouble"

49904,0,0,0,0,0,0,0,"Gemstone Shortage"
49905,0,0,0,0,0,0,0,"Gemstone Shortage"
49906,0,0,0,0,0,0,0,"Gemstone Shortage"

49907,0,0,0,0,0,0,0,"Poring Problem"
49908,0,1002,10,0,0,0,0,"Poring Problem"
49909,0,0,0,0,0,0,0,"Poring Problem"

49910,0,0,0,0,0,0,0,"Hand over the Artifact"
49911,15,0,0,0,0,0,0,"Hand over the Artifact"
49912,0,0,0,0,0,0,0,"Hand over the Artifact"
49913,0,0,0,0,0,0,0,"Hand over the Artifact"
*/

/* client\data\questid2display.txt
49900#acolyte trouble#SG_FEEL#QUE_GOD02#
Talk to the acolyte inside prontera church#
Talk to the acolyte#

49901#acolyte trouble#SG_FEEL#QUE_NOIMAGE#
Journey to Izlude town and talk to acolyte's mother#
Find old lady in izlude#

49902#acolyte trouble#SG_FEEL#QUE_NOIMAGE#
Go back to Prontera Church and hand over the letter to the acolyte#
Talk to the acolyte#

49903#acolyte trouble#SG_FEEL#QUE_NOIMAGE#
Mission COMPLETE !#
Submit to Tavern Master#

49904#gemstone shortage#SG_FEEL#QUE_NOIMAGE#
Talk to gemstone seller#
Talk to gemstone seller#

49905#gemstone shortage#SG_FEEL#QUE_NOIMAGE#
collect 10 yellow gemstones, 10 blue gemstones, 10 red gemstones and bring them to gem seller#
Collect gemstones#

49906#gemstone shortage#SG_FEEL#QUE_NOIMAGE#
Mission COMPLETE !#
Submit to Tavern Master#

49907#poring problem#SG_FEEL#QUE_NOIMAGE#
Talk to Prontera guard#
Talk to Prontera guard#

49908#poring problem#SG_FEEL#QUE_NOIMAGE#
Poring has invaded Prontera town last night, help out the town by killing porings outside the town#
kill 10 porings#

49909#poring problem#SG_FEEL#QUE_NOIMAGE#
Mission COMPLETE !#
Submit to Tavern Master#

49910#hand over the artifact#SG_FEEL#QUE_NOIMAGE#
There is a rare artifact in the town kept by artifact collecter. Hand over the artifact to Tavern Master#
Talk to artifact collecter#

49911#hand over the artifact#SG_FEEL#QUE_NOIMAGE#
Talk to assistant FAST! Time is running out!#
Talk to assistant FAST!#

49912#hand over the artifact#SG_FEEL#QUE_NOIMAGE#
Return to artifact collecter#
Talk to artifact collecter#

49913#hand over the artifact#SG_FEEL#QUE_NOIMAGE#
Mission COMPLETE !#
Submit to Tavern Master#
*/

/*
mission 1 - acolyte trouble
49900 - from tavern, talk to acolyte
49901 - from acolyte, talk to npc in izlude
49902 - from izlude, talk to acolyte
49903 - from acolyte, submit to tavern

mission 2 - gemstone stortage
49904 - from tavern, talk to gem seller
49905 - from gem seller, collect 10 yellow/blue/red gemstone
49906 - from gem seller, submit to tavern

mission 3 - poring problem
49907 - from tavern, talk to guard
49908 - from guard, kill 10 poring
49909 - after kill poring and talked to guard, submit to tavern

mission 4 - hand over the artifact
49910 - from tavern, talk to artifact collecter
49911 - from artifact collecter, talk to assistant within 20 seconds
49912 - from assistant, talk to artifact collecter
49913 - from artifact collecter, submit to tavern
*/

prontera,153,180,5	script	reset quest(debug)	100,{
	for ( .@i = 49900; .@i < 49913; .@i++ )
		if ( checkquest(.@i) != -1 )
			erasequest .@i;
}

prontera,156,180,5	script	Prontera Tavern	100,{
	mes "take a mission";
	next;
	switch ( select ( "acolyte trouble","gemstone shortage","poring problem","hand over the artifact" ) ) {
		case 1:
			if ( checkquest(49903) == 2 ) {
				mes "you already completed this mission";
				close;
			}
			else if ( checkquest(49903) == 1 ) {
				mes "you complete this mission!";
				dispbottom "mission 1 pass";
				completequest 49903;
				close;
			}
			else if ( checkquest(49900) == 1 || checkquest(49901) == 1 || checkquest(49902) == 1 ) {
				mes "you still on the mission";
				close;
			}
			mes "all right, talk to the <NAVI>Acolyte<INFO>prt_church,102,77,</INFO></NAVI> from the church.";
			setquest 49900;
			close;
		case 2:
			if ( checkquest(49906) == 2 ) {
				mes "you already completed this mission";
				close;
			}
			else if ( checkquest(49906) == 1 ) {
				mes "you complete this mission!";
				dispbottom "mission 2 pass";
				completequest 49906;
				close;
			}
			else if ( checkquest(49904) == 1 || checkquest(49905) == 1 ) {
				mes "you still on the mission";
				close;
			}
			mes "all right, talk to the <NAVI>gemstone seller<INFO>prontera,248,151,</INFO></NAVI>.";
			setquest 49904;
			close;
		case 3:
			if ( checkquest(49909) == 2 ) {
				mes "you already completed this mission";
				close;
			}
			else if ( checkquest(49909) == 1 ) {
				mes "you complete this mission!";
				dispbottom "mission 3 pass";
				completequest 49909;
				close;
			}
			else if ( checkquest(49907) == 1 || checkquest(49908) == 1 ) {
				mes "you still on the mission";
				close;
			}
			mes "all right, talk to <NAVI>the guard<INFO>prontera,147,28,</INFO></NAVI>.";
			setquest 49907;
			close;
		case 4:
			if ( checkquest(49913) == 2 ) {
				mes "you already completed this mission";
				close;
			}
			else if ( checkquest(49913) == 1 ) {
				mes "you complete this mission!";
				dispbottom "mission 4 pass";
				completequest 49913;
				close;
			}
			else if ( checkquest(49910) == 1 || checkquest(49911) == 1 || checkquest(49912) == 1 ) {
				mes "you still on the mission";
				close;
			}
			mes "all right, talk to the <NAVI>artifact collecter<INFO>prontera,57,293,</INFO></NAVI>.";
			setquest 49910;
			close;
		}
	end;
}

prt_church,102,77,3	script	acolyte	95,{
	if ( checkquest(49902) == 1 ) {
		mes "thx! a letter from my mum!";
		mes "from now on I'll do my best in training!";
		changequest 49902,49903;
		close;
	}
	else if ( checkquest(49901) == 1 ) {
		mes "can you please tell <NAVI>my mum<INFO>izlude,88,139,</INFO></NAVI> how much I miss her?";
		close;
	}
	else if ( checkquest(49900) == 1 ) {
		mes "I miss <NAVI>my mum<INFO>izlude,88,139,</INFO></NAVI> day and night ... can you please go to my hometown in izlude and tell her how much I miss her?";
		changequest 49900,49901;
		close;
	}
	mes "I must focus on my training!";
	close;
}

izlude,88,139,5	script	old woman	773,{
	if ( checkquest(49901) == 1 ) {
		mes "<NAVI>my child<INFO>prt_church,102,77,</INFO></NAVI> ask you to come? that's great! can you deliver this letter to him now?";
		changequest 49901,49902;
		close;
	}
	mes "I miss my child...";
	close;
}

prontera,248,151,3	script	gemstone seller	58,{
	if ( checkquest(49905) == 1 ) {
		if ( countitem(715) >= 10 && countitem(716) >= 10 && countitem(717) >= 10 ) {
			mes "you got it! now we can replenish our stock!";
			delitem 715,10;
			delitem 716,10;
			delitem 717,10;
			changequest 49905,49906;
			close;
		}
		mes "you still haven't gathered enough gemstones?";
		close;
	}
	else if ( checkquest(49904) == 1 ) {
		mes "yes, our town need more gemstones. Can you help resupply our shortage of gemstones?";
		changequest 49904,49905;
		close;
	}
	mes "we need more gem stones!";
	close;
}

prontera,147,28,5	script	Guard	105,{
	if ( checkquest(49908) == 1 ) {
		if ( checkquest(49908,HUNTING) == 2 ) {
			mes "you've kill enough porings! you're our hero";
			changequest 49908,49909;
			close;
		}
		mes "you haven't done killing yet";
		close;
	}
	else if ( checkquest(49907) == 1 ) {
		mes "yesterday night this town was invaded by porings, can you eliminate them?";
		changequest 49907,49908;
		close;
	}
	mes "hi, welcome to prontera town";
	close;
}

prontera,57,293,5	script	artifact collecter	47,{
	if ( checkquest(49912) == 1 ) {
		mes "yes! that's the one! now you can hand it over to <NAVI>tavern master<INFO>prontera,156,180,</INFO></NAVI>";
		changequest 49912,49913;
		close;
	}
	else if ( checkquest(49911) == 1 ) {
		mes "FAST! she is about to throw it away!";
		close;
	}
	else if ( checkquest(49910) == 1 ) {
		mes "ahh ... that artifact tavern master requested ... wait! I just asked <NAVI>my assistant<INFO>prontera,31,158,</INFO></NAVI> to throw it! Faster stop her before its too late";
		dispbottom "you have 15 seconds to retrieve it";
		close2;
		changequest 49910,49911;
		end;
	}
	mes "today a sunny day....";
	close;
}

prontera,31,158,5	script	assistant	66,{
	if ( checkquest(49911) == 1 ) {
		if ( checkquest(49911,PLAYTIME) == 2 ) {
			mes "oh no, the rubbish truck just send them away ...";
			dispbottom "you failed the quest";
			erasequest 49911;
			close;
		}
		mes "you're just in time! here the artifact you requested. But to be sure, double check with <NAVI>my master<INFO>prontera,57,293,</INFO></NAVI>.";
		changequest 49911,49912;
		close;
	}
	mes "work~ work~ work~";
	close;
}