//===== Hercules Script ======================================
//= Dressing Girl
//===== By: ==================================================
//= AnnieRuru
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: ===================================== 
//= Hercules 2020-10-11
//===== Description: =========================================
//= Let player try out various headgear before they decided to buy them
//===== Topic ================================================
//= http://herc.ws/board/topic/11182-dressing-room-npc/
//===== Additional Comments: =================================
//= addtimer hack !
//============================================================

prontera,155,185,5	script	Dressing Girl	1_F_MARIA,{
	addtimer 1, strnpcinfo(NPC_NAME)+"::OnChangeBack";
	mes "[Dressing Girl]";
	mes "do you want to try out the look of some awesome headgear?";
	for (.@i = 0; .@i < .headgearsize; ++.@i)
		mes F_MesItemInfo(.headgear[.@i]);
	next;
	.@s = select(.menu$) -1;
	if (getiteminfo(.headgear[.@s], ITEMINFO_LOC) & EQP_HEAD_TOP)
		changelook LOOK_HEAD_TOP, getiteminfo(.headgear[.@s], ITEMINFO_VIEWSPRITE);
	else if (getiteminfo(.headgear[.@s], ITEMINFO_LOC) & EQP_HEAD_MID)
		changelook LOOK_HEAD_MID, getiteminfo(.headgear[.@s], ITEMINFO_VIEWSPRITE);
	else if (getiteminfo(.headgear[.@s], ITEMINFO_LOC) & EQP_HEAD_LOW)
		changelook LOOK_HEAD_BOTTOM, getiteminfo(.headgear[.@s], ITEMINFO_VIEWSPRITE);
	else if (getiteminfo(.headgear[.@s], ITEMINFO_LOC) & EQP_GARMENT)
		changelook LOOK_ROBE, getiteminfo(.headgear[.@s], ITEMINFO_VIEWSPRITE);
	else {
		mes "this is not a headgear ...";
		close;
	}
	mes "[Dressing Girl]";
	mes "that look nice on you";
	close;
OnChangeBack:
	.@top = getequipid(EQI_HEAD_TOP);
	changelook LOOK_HEAD_TOP, (.@top == -1)? 0 : getiteminfo(.@top, ITEMINFO_VIEWSPRITE);
	.@mid = getequipid(EQI_HEAD_MID);
	changelook LOOK_HEAD_MID, (.@mid == -1)? 0 : getiteminfo(.@mid, ITEMINFO_VIEWSPRITE);
	.@low = getequipid(EQI_HEAD_LOW);
	changelook LOOK_HEAD_BOTTOM, (.@low == -1)? 0 : getiteminfo(.@low, ITEMINFO_VIEWSPRITE);
	.@robe = getequipid(EQI_GARMENT);
	changelook LOOK_ROBE, (.@robe == -1)? 0 : getiteminfo(.@robe, ITEMINFO_VIEWSPRITE);
	end;
OnInit:
	setarray .headgear, // add as many as possible
		Archangel_Wing,
		Red_Tailed_Ribbon,
		Charming_Ribbon,
		Twin_Ribbon,
		Ribbon_Pink,
		Ribbon_Of_Bride,
		Big_Sis_Ribbon,
		Fillet,
		Pair_Of_Red_Ribbon,
		Br_Twin_Ribbon,
		Blush_Of_Groom,
		Glasses;
	.headgearsize = getarraysize(.headgear);
	for (.@i = 0; .@i < .headgearsize; ++.@i)
		.menu$ += getitemname(.headgear[.@i]) +":";
	end;
}