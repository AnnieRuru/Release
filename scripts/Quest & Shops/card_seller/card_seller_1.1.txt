//===== Hercules Script ======================================
//= Card Seller A-Z
//===== By: ==================================================
//= AnnieRuru
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: ===================================== 
//= Hercules 2019-02-25
//===== Description: =========================================
//= Sells all cards dropped by mobs, grouped alphabetically.
//===== Topic ================================================
//= http://herc.ws/board/topic/16586-card-seller-a-z/
//===== Additional Comments: =================================
//= Use Hercules NPC Trader, with functions
//============================================================

prontera,155,177,5	script	Card Seller	1_F_MARIA,{
	mes "Welcome to card seller ... meh lazy to say";
	next;
	if ( select ( "Normal Cards", "Mini-boss Cards", "MVP Cards" ) == 1 ) {
		.@s = select( .alphabet_menu$ ) -1;
		close2;
		openshop "card_mob#"+ .alphabet$[.@s];
		dispbottom sprintf( _$( "You currently have %d %s." ), countitem( Monster_Ticket_ ), getitemname( Monster_Ticket_ ) ), C_SPRINGGREEN;
		end;
	}
	close2;
	if ( @menu == 2 ) {
		openshop "card_mob#miniboss";
		dispbottom sprintf( _$( "You currently have %d %s." ), countitem( Mini_Boss_Ticket ), getitemname( Mini_Boss_Ticket ) ), C_SPRINGGREEN;
	}
	else {
		openshop "card_mob#MVP";
		dispbottom sprintf( _$( "You currently have %d %s." ), countitem( MVP_Ticket ), getitemname( MVP_Ticket ) ), C_SPRINGGREEN;
	}
	end;
OnInit:
	.@total = query_sql("select distinct left( name_japanese, 1 ) as aaa from item_db right join mob_db on item_db.id = mob_db.dropcardid where ~mode & 32 and type = 6 group by name_japanese order by aaa", .alphabet$ );
	for ( .@i = 0; .@i < .@total; ++.@i )
		.alphabet_menu$ += .alphabet$[.@i] +" Cards:";
	end;
}

function	script	F_CardSeller_Init	{
	tradertype NST_CUSTOM;
	.@type$ = strnpcinfo(NPC_NAME_HIDDEN);
	if ( .@type$ == "miniboss" ) {
		.@nb = query_sql( "select item_db.id from item_db right join mob_db on item_db.id = mob_db.dropcardid where mode & 32 and type = 6 and mexp = 0 and item_db.id not in ( select item_db.id from item_db right join mob_db on item_db.id = mob_db.dropcardid where ~mode & 32 and type = 6 ) and item_db.id not in ( select item_db.id from item_db right join mob_db on item_db.id = mob_db.dropcardid where mode & 32 and type = 6 and mexp != 0 ) group by item_db.id order by name_japanese", .@id );
		.@price = 1; // change the price here
	}
	else if ( .@type$ == "MVP" ) {
		.@nb = query_sql( "select item_db.id from mob_db right join item_db on item_db.id = mob_db.dropcardid where mode & 32 and type = 6 and mexp != 0 and item_db.id not in ( select item_db.id from item_db right join mob_db on item_db.id = mob_db.dropcardid where ~mode & 32 and type = 6 ) group by item_db.id order by name_japanese", .@id );
		.@price = 1;
	}
	else {
		.@nb = query_sql( "select item_db.id from item_db right join mob_db on item_db.id = mob_db.dropcardid where ~mode & 32 and type = 6 and left( name_japanese, 1 ) = '"+ .@type$ +"' group by name_japanese order by name_japanese", .@id );
		.@price = 1;
	}
	for ( .@i = 0; .@i < .@nb; ++.@i )
		sellitem .@id[.@i], .@price;
	end;
}

function	script	F_CardSeller_Count	{
	.@type$ = strnpcinfo(NPC_NAME_HIDDEN);
	if ( .@type$ == "miniboss" )
		setcurrency Mini_Boss_Ticket, 0; // change the Item currency here, although better use replace all in a file
	else if ( .@type$ == "MVP" )
		setcurrency MVP_Ticket, 0;
	else
		setcurrency Monster_Ticket_, 0;
	end;
}

function	script	F_CardSeller_Pay	{
	.@type$ = strnpcinfo(NPC_NAME_HIDDEN);
	if ( .@type$ == "miniboss" )
		.@currency = Mini_Boss_Ticket;
	else if ( .@type$ == "MVP" )
		.@currency = MVP_Ticket;
	else
		.@currency = Monster_Ticket_;
    if ( countitem( .@currency ) < @price )
        end;
    delitem .@currency, @price;
    purchaseok();
    end;
}
-	trader	card_mob#A	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#B	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#C	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#D	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#E	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#F	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#G	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#H	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#I	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#J	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#K	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#L	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#M	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#N	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#O	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#P	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#Q	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#R	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#S	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#T	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#U	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#V	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#W	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#X	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#Y	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#Z	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#miniboss	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
-	trader	card_mob#MVP	FAKE_NPC,{ OnInit: F_CardSeller_Init; OnCountFunds: F_CardSeller_Count; OnPayFunds: F_CardSeller_Pay; }
