//===== eAthena Script =======================================
//= WoW domination Battleground : Arathi Basin
//===== By: ==================================================
//= AnnieRuru
//===== Current Version: =====================================
//= 0.2
//===== Compatible With: ===================================== 
//= eAthena 2011-05-19
//===== Description: =========================================
//= 1st version
//===== Topic ================================================
//= https://www.eathena.ws/board/index.php?s=&showtopic=272293&view=findpost&p=1492583
//===== Additional Comments: =================================  
//= Thanks to Brainstorm, for the mathematics calculations ;-)
//============================================================

-	script	wow_domi#controller	-1,{
OnInit:
	set .scoretowin, 100; // 2000 points to win
	set .time2capture, 3; // 10 seconds require to capture the flag

	set .tick[1], 12000;
	set .tick[2], 9000;
	set .tick[3], 6000;
	set .tick[4], 3000;
	set .tick[5], 1000;

	set .point[1], 1;
	set .point[2], 1;
	set .point[3], 1;
	set .point[4], 1;
	set .point[5], 3;

	set .tick[0], 2000000000;
	set .point[0], 0;
	bg_updatescore "guild_vs2", 0, 0;
	end;
Onstart:
	if ( getwaitingroomstate( 0, "Alliance" ) == 1 && getwaitingroomstate( 0, "Horde" ) == 1 ) {
		set .team1, waitingroom2bg("guild_vs2",50,92,"wow_domi#controller::OnAllianceQuit","wow_domi#controller::OnAllianceDie", "Alliance");
		set .team2, waitingroom2bg("guild_vs2",50,7,"wow_domi#controller::OnHordeQuit","wow_domi#controller::OnHordeDie", "Horde");
		bg_warp .team1, "guild_vs2", 50,92;
		bg_warp .team2, "guild_vs2", 50,7;
		donpcevent "wow_domi_score_team#1::Onstart";
		donpcevent "wow_domi_score_team#2::Onstart";
		disablenpc "Alliance";
		disablenpc "Horde";
	}
	end;
OnAllianceDie: // insert stuff here
	end;
OnHordeDie: // insert stuff here
	end;
OnAllianceQuit:
OnHordeQuit:
	bg_leave;
	end;
}

prontera,153,175,5	script	Alliance	100,{
	end;
OnInit:
	waitingroom "Alliance",1,"wow_domi#controller::Onstart",1;
	end;
}
prontera,158,175,5	script	Horde	100,{
	end;
OnInit:
	waitingroom "Horde",1,"wow_domi#controller::Onstart",1;
	end;
}
-	script	Flag#0::wow_domi_flag	-1,{
	if ( getcharid(4) != getvariableofnpc( .team1, "wow_domi#controller" ) && getcharid(4) != getvariableofnpc( .team2, "wow_domi#controller" ) ) {
		dispbottom "out of the way";
		end;
	}
	if ( getcharid(4) == getvariableofnpc( .team1, "wow_domi#controller" ) && getvariableofnpc( .flag[ atoi( strnpcinfo(2) ) ], "wow_domi#controller" ) == 1 || getcharid(4) == getvariableofnpc( .team2, "wow_domi#controller" ) && getvariableofnpc( .flag[ atoi( strnpcinfo(2) ) ], "wow_domi#controller" ) == 2 ) {
		dispbottom "your team already capture this point";
		end;
	}
	getmapxy .@map$, .@x, .@y, 0;
	getmapxy .@map$, .@x1, .@y1, 1;
	if ( distance( .@x, .@y, .@x1, .@y1 ) > 5 ) {
		dispbottom "too far";
		end;
	}
	else if ( checkoption(0x4046) ) {
		dispbottom "you can't capture a flag while cloaking or hiding";
		end;
	}
	progressbar "0x0000FF", getvariableofnpc( .time2capture, "wow_domi#controller" );
	if ( getcharid(4) == getvariableofnpc( .team1, "wow_domi#controller" ) && getvariableofnpc( .flag[ atoi( strnpcinfo(2) ) ], "wow_domi#controller" ) == 1 || getcharid(4) == getvariableofnpc( .team2, "wow_domi#controller" ) && getvariableofnpc( .flag[ atoi( strnpcinfo(2) ) ], "wow_domi#controller" ) == 2 ) {
		dispbottom "your team already capture this point";
		end;
	}
	if ( getcharid(4) == getvariableofnpc( .team1, "wow_domi#controller" ) )
		set .@team, 1;
	else
		set .@team, 2;
	set getvariableofnpc( .flag[ atoi( strnpcinfo(2) ) ], "wow_domi#controller" ), .@team;
	setnpcdisplay strnpcinfo(0), ( .@team == 1 )? 973 : 974;
	announce ( ( .@team == 1 )?"Alliance":"Horde" )+" has captured "+ strnpcinfo(1), 1;
	awake "wow_domi_score_team#"+ .@team;
	end;
}
guild_vs2,49,69,4	duplicate(wow_domi_flag)	Stable#1	722
guild_vs2,72,50,4	duplicate(wow_domi_flag)	Gold Mine#2	722
guild_vs2,49,49,4	duplicate(wow_domi_flag)	Blacksmith#3	722
guild_vs2,26,49,4	duplicate(wow_domi_flag)	Lumber Mill#4	722
guild_vs2,49,28,4	duplicate(wow_domi_flag)	Farm#5	722

-	script	wow_domi_score_team	-1,{
Onstart:
	set .@t, strnpcinfo(2);
	while ( getvariableofnpc( .stop, "wow_domi#controller" ) == 0 ) {
		set .@catched, 0;
		for ( set .@i, 1; .@i <= 5; set .@i, .@i +1 )
			if ( getvariableofnpc( .flag[.@i], "wow_domi#controller" ) == .@t )
				set .@catched, .@catched +1;
		set .@tick, getvariableofnpc( .tick[ .@catched ], "wow_domi#controller" );
		set getvariableofnpc( .score[.@t], "wow_domi#controller" ), getvariableofnpc( .score[.@t], "wow_domi#controller" ) + getvariableofnpc( .point[ .@catched ], "wow_domi#controller" );
		if ( getvariableofnpc( .score[.@t], "wow_domi#controller" ) >= getvariableofnpc( .scoretowin, "wow_domi#controller" ) ) {
			set getvariableofnpc( .stop, "wow_domi#controller" ), 1;
			announce "Team "+ .@t +" won!", 0;
			for ( set .@i, 1; .@i <= 5; set .@i, .@i +1 )
				set getvariableofnpc( .flag[.@i], "wow_domi#controller" ), 0;
			setnpcdisplay "Stable#1", 722;
			setnpcdisplay "Gold Mine#2", 722;
			setnpcdisplay "Blacksmith#3", 722;
			setnpcdisplay "Lumber Mill#4", 722;
			setnpcdisplay "Farm#5", 722;
			set getvariableofnpc( .score[1], "wow_domi#controller" ), 0;
			set getvariableofnpc( .score[2], "wow_domi#controller" ), 0;
			enablenpc "Alliance";
			enablenpc "Horde";
			awake "wow_domi_score_team#1";
			awake "wow_domi_score_team#2";
			sleep 3000;
			bg_warp getvariableofnpc( .team1, "wow_domi#controller" ), "Save", 0,0;
			bg_warp getvariableofnpc( .team2, "wow_domi#controller" ), "Save", 0,0;
			bg_destroy getvariableofnpc( .team1, "wow_domi#controller" );
			bg_destroy getvariableofnpc( .team2, "wow_domi#controller" );
			bg_updatescore "guild_vs2", 0, 0;
			break;
		}
		if ( .@score != getvariableofnpc( .score[.@t], "wow_domi#controller" ) )
			bg_updatescore "guild_vs2", getvariableofnpc( .score[1], "wow_domi#controller" ), getvariableofnpc( .score[2], "wow_domi#controller" );
		set .@score, getvariableofnpc( .score[.@t], "wow_domi#controller" );
		sleep .@tick;
	}
	end;
}
-	duplicate(wow_domi_score_team)	wow_domi_score_team#1	-1
-	duplicate(wow_domi_score_team)	wow_domi_score_team#2	-1

guild_vs2,50,92,5	script	Alliance Flag	973,{}
guild_vs2,50,7,5	script	Horde Flag	974,{}

guild_vs2	mapflag	battleground	2
guild_vs2	mapflag	nowarp
guild_vs2	mapflag	nowarpto
guild_vs2	mapflag	nomemo
guild_vs2	mapflag	noteleport



//	Debug only
-	script	asdfasdf	-1,{
OnPCLoadMapEvent:
	if ( strcharinfo(3) == "prontera" )
		bg_leave;
	end;
}
prontera	mapflag	loadevent