//===== Hercules Script ======================================
//= Monthly MVP Rank
//===== By: ==================================================
//= AnnieRuru
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: ===================================== 
//= Hercules 2015-12-25
//===== Description: =========================================
//= MVP Ranking script, with sendmail the reward monthly
//===== Topic ================================================
//= http://herc.ws/board/topic/4552-mvp-ladder-script-enhancement/
//===== Additional Comments: =================================  
//= remember to change the reward item at setarray .@reward
//============================================================

/*
alter table `char` add column mvp_rank int(11) default 0 after delete_date, add index (mvp_rank);

create table mvp_rank_archive (
`date` datetime,
pos tinyint(4) unsigned,
cid int(11) unsigned default 0,
name varchar(23) default '',
points int(11) unsigned not null,
primary key ( `date`, pos )
) engine innodb;
*/

prontera,158,183,3	script	MVP Ranking	1_F_MARIA,{
	mes "[MVP Ranking]";
	mes "Hello!";
	mes "What are you doing here?";
	next;
	switch(select("Check rankings.:My stats.:Previous Month ranking")) {
	case 1:
		mes "[MVP Ranking]";
		mes "Rankings :->";
		.@nb = query_sql( "select name, mvp_rank from `char` where mvp_rank > 0 order by mvp_rank desc limit 10", .@name$, .@count );
		if ( !.@nb ) {
			mes "no entry";
			close;
		}
		for ( .@i = 0; .@i < .@nb; ++.@i )
			mes ( .@i +1 )+". "+ .@name$[.@i] +" -> "+ .@count[.@i] +" points";
		close;
	case 2:
		query_sql "select mvp_rank from `char` where char_id = "+ getcharid(0), .@killed;
		mes "[MVP Ranking]";
		mes "You have killed "+ .@killed +" MVPs";
		close;
	case 3:
		mes "[MVP Ranking]";
		mes "Last month ranking";
		if ( gettime(GETTIME_MONTH) > 10 )
			.@store_date$ = gettime(GETTIME_YEAR) +"-"+( gettime(GETTIME_MONTH) -1 )+"-00 00:00:00";
		else if ( gettime(GETTIME_MONTH) > 1 )
			.@store_date$ = gettime(GETTIME_YEAR) +"-0"+( gettime(GETTIME_MONTH) -1 )+"-00 00:00:00";
		else
			.@store_date$ = ( gettime(GETTIME_YEAR) -1 ) +"-12-00 00:00:00";
		.@nb = query_sql( "select name, points from mvp_rank_archive where `date` = '"+ .@store_date$ +"' order by pos", .@name$, .@count );
		if ( !.@nb ) {
			mes "no entry";
			close;
		}
		for ( .@i = 0; .@i < .@nb; ++.@i )
			mes ( .@i +1 )+". "+ .@name$[.@i] +" -> "+ .@count[.@i] +" points";
		close;
	}
	close;
OnInit:
	if ( $mvp_ladder_last_given == atoi( gettime(GETTIME_YEAR) +""+ gettime(GETTIME_MONTH) ) ) end;
	goto L_give;
OnClock0000:
	if ( gettime(GETTIME_DAYOFMONTH) != 1 ) end;
L_give:
	.@nb = query_sql( "select char_id, name, mvp_rank from `char` where mvp_rank > 0 order by mvp_rank desc limit 10", .@cid, .@name$, .@killed );
	if ( !.@nb ) end;
	setarray .@reward, 501, 502, 503, 504, 505, 506, 507, 508, 509, 510;
	setarray .@amount, 10, 9, 8, 7, 6, 5, 4, 3, 2, 1;
	for ( .@i = 0; .@i < .@nb; ++.@i )
		sendmail .@cid[.@i], .@name$[.@i], "[MVP RANK]", "Congratulations for earning No. "+( .@i +1 )+" position in MVP rank. Here is your reward.", 0, .@reward[.@i], .@amount[.@i];
	if ( gettime(GETTIME_MONTH) > 10 )
		.@store_date$ = gettime(GETTIME_YEAR) +"-"+( gettime(GETTIME_MONTH) -1 )+"-00 00:00:00";
	else if ( gettime(GETTIME_MONTH) > 1 )
		.@store_date$ = gettime(GETTIME_YEAR) +"-0"+( gettime(GETTIME_MONTH) -1 )+"-00 00:00:00";
	else
		.@store_date$ = ( gettime(GETTIME_YEAR) -1 ) +"-12-00 00:00:00";
	for ( .@i = 0; .@i < .@nb; ++.@i )
		.@store$[.@i] = "( '"+ .@store_date$ +"', "+( .@i +1 )+", "+ .@cid[.@i] +", '"+ escape_sql( .@name$[.@i] ) +"', "+ .@killed[.@i] +" )";
	query_sql "insert into mvp_rank_archive values "+ implode( .@store$, "," );
	query_sql "update `char` set mvp_rank = 0"; // reset the rank
	$mvp_ladder_last_given = atoi( gettime(GETTIME_YEAR) +""+ gettime(GETTIME_MONTH) );
	end;
OnNPCKillEvent:
	if ( getmonsterinfo( killedrid, MOB_MVPEXP ) ) {
		query_sql "update `char` set mvp_rank = mvp_rank +1 where char_id = "+ getcharid(0);
		query_sql "select mvp_rank from `char` where char_id = "+ getcharid(0), .@killed;
		dispbottom "~ You've killed "+ .@killed +" MVP. ~";
		specialeffect2 EF_HEAL2;
	}
	end;
}