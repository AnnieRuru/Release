function	script	F_MesItemInfo	{
	.@item = getarg(0);
	.@itemname$ = getitemname(.@item);
	if (.@itemname$ != "null") {
		.@itemslot = getitemslots(.@item);
		if (.@itemslot)
			.@itemname$ = sprintf("%s [%d]", .@itemname$, .@itemslot);
	}
	else
		.@itemname$ = "Unknown Item";
	if (PACKETVER >= 20150729)
		return sprintf("<ITEM>%s<INFO>%d</INFO></ITEM>", .@itemname$, .@item);
	else if (PACKETVER >= 20130130)
		return sprintf("<ITEMLINK>%s<INFO>%d</INFO></ITEMLINK>", .@itemname$, .@item);
	else
		return .@itemname$;
}

//===== rAthena Script =======================================
//= 3 cards of your choice = 1 random card
//===== By: ==================================================
//= AnnieRuru
//===== Current Version: =====================================
//= 0.2
//===== Compatible With: ===================================== 
//= rAthena 2020-10-15
//===== Description: =========================================
//= also added no MVP card in the list
//===== Topic ================================================
//= https://rathena.org/board/topic/126214-disable-mini-boss-and-boss-card-in-card-trader/
//===== Additional Comments: =================================  
//= add blacklist
//============================================================

prontera,151,183,6	script	Sample	1_F_MARIA,{
	getinventorylist;
	for (.@i = 0; .@i < @inventorylist_count; ++.@i) {
		if (.check_card[@inventorylist_id[.@i]]) {
			.@menu$ += getitemname(@inventorylist_id[.@i]) + ":";
			.@card[@inventorylist_id[.@i]] = @inventorylist_amount[.@i];
			.@index[.@c++] = .@i;
			.@check_card_count += @inventorylist_amount[.@i];
		}
	}
	if (.@check_card_count < 3) {
		mes "You need at least 3 cards to deal with me.";
		close;
	}
	mes "Please select 3 cards...";
	.@c = 0;
	for (.@i = 0; .@i < 3; ++.@i) {
		.@s = select(.@menu$) -1;
		.@card_chosen[.@c++] = .@tmp = @inventorylist_id[.@index[.@s]];
		if (--.@card[.@tmp] == 0)
			.@menu$ = replacestr(.@menu$, getitemname(.@tmp), "");
		mes "> 1x " + F_MesItemInfo(.@tmp);
	}
	mes "Are you sure you want to trade these cards?";
	if (select("~ Yes", "~ No") == 2) end;
	for (.@i = 0; .@i < 3; ++.@i)
		delitem .@card_chosen[.@i], 1;
	.@get_cardid = .cardid[rand(.size)];
	mes "You get "+ F_MesItemInfo(.@get_cardid);
	getitem .@get_cardid, 1;
	close;
OnInit:
	setarray .@blacklist, 4001, 4002, 4003; // add more

	freeloop true;
	.@size = getarraysize(.@blacklist);
	for (.@i = 0; .@i < .@size; ++.@i)
		.@blacklist$ = .@blacklist;
	.@query$  = "SELECT `item_db_re`.`id` ";
	.@query$ += "FROM `item_db_re` RIGHT JOIN `mob_db_re` ON `item_db_re`.`id` = `mob_db_re`.`DropCardid` ";
	.@query$ += "WHERE (mode & "+ MD_MVP +") = 0 AND `DropCardid` != "+ false +" AND `type` = "+ IT_CARD +" AND RIGHT(`name_japanese`, 4) = 'Card' AND `item_db_re`.`id` NOT IN ("+ implode(.@blacklist$, ",")+") GROUP BY 1";
	.size = query_sql(.@query$, .cardid);
	for (.@i = 0; .@i < .size; ++.@i)
		.check_card[.cardid[.@i]] = true;
}
