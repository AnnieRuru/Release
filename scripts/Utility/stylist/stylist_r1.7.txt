//===== rAthena Script =======================================
//= Stylist npc
//===== By: ==================================================
//= AnnieRuru
//= original by Euphy
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena 2020-10-16
//===== Description: =========================================
//= stylist npc
//===== Topic ================================================
//= https://rathena.org/board/topic/126230-help-stylist-restriction-bodystyle-2/
//===== Additional Comments: =================================  
//= add reverse order, add changelook and set this style
//============================================================

prontera,170,180,1	script	Stylist	2_M_DYEINGER,{
	.@choose = select( "Hair style", "Hair color", "Cloth color", (!(eaclass() & EAJL_THIRD) || BaseJob == Job_SuperNovice)? "": "Body Style") -1;
	.@lookpart = .@part = .look[.@choose];
	if (BaseClass == Job_Summoner)
		.@part += Job_Summoner;
	.@revert = .@i = inarray(getd(".list"+ .@part), getlook(.@lookpart));
	setpcblock PCBLOCK_COMMANDS, true;
	while (true) {
		changelook .@lookpart, getd(".list"+ .@part +"["+ .@i +"]");
		message strcharinfo(0), "This is style #"+ getd(".list"+ .@part +"["+ .@i +"]") +".";
		.@next = (.@i == .maxindex[.@part])? 0 : .@i+1;
		.@previous = (.@i == 0)? .maxindex[.@part] : .@i-1;
		.@next_option_menu$ = " ~ Next (^0055FF"+ getd(".list"+ .@part +"["+ .@next +"]") +"^000000)";
		.@previous_option_menu$ = " ~ Previous (^0055FF"+ getd(".list"+ .@part +"["+ .@previous +"]") +"^000000)";
		switch(prompt(
			(.@reverse_order)? .@previous_option_menu$ : .@next_option_menu$,
			(.@reverse_order)? .@next_option_menu$ : .@previous_option_menu$,
			" ~ Jump to...",
			" ~ Revert to original (^0055FF"+ getd(".list"+ .@part +"["+ .@revert +"]") +"^000000)",
			"~ Use this style")) {
		case 1:
			.@i = (.@reverse_order)? .@previous : .@next;
			break;
		case 2:
			.@i = (.@reverse_order)? .@next : .@previous;
			.@reverse_order = !.@reverse_order;
			break;
		case 3:
			message strcharinfo(0), "Choose a style between "+ .minstyles[.@part] +" - "+ .maxstyles[.@part] +".";
			input .@num;
			.@j = inarray( getd(".list"+ .@part), .@num );
			if ( .@j == -1 )
				message strcharinfo(0), "That style is unavailable";
			else
				.@i = .@j;
			break;
		case 4:
			.@i = .@revert;
			break;
		case 5:
			setlook .@lookpart, getd(".list"+ .@part +"["+ .@i +"]");
			setpcblock PCBLOCK_COMMANDS, false;
			close;
		case 255:
			changelook .@lookpart, getd(".list"+ .@part +"["+ .@revert +"]");
			setpcblock PCBLOCK_COMMANDS, false;
			close;
		}
	}
	end;
OnInit:
//	.list$[LOOK_HAIR] = "0-5,11-18,20-23";
	.list$[LOOK_HAIR] = getbattleflag("min_hair_style") +"-"+ getbattleflag("max_hair_style");
	.list$[LOOK_HAIR_COLOR] = getbattleflag("min_hair_color") +"-"+ getbattleflag("max_hair_color");
	.list$[LOOK_CLOTHES_COLOR] = getbattleflag("min_cloth_color") +"-"+ getbattleflag("max_cloth_color");
//	.list$[LOOK_BODY2] = getbattleflag("min_body_style") +"-"+ getbattleflag("max_body_style");
	.list$[LOOK_BODY2] = "0-1";

	.list$[Job_Summoner + LOOK_HAIR] = getbattleflag("min_hair_style") +"-"+ getbattleflag("max_hair_style");
	.list$[Job_Summoner + LOOK_HAIR_COLOR] = getbattleflag("min_hair_color") +"-"+ getbattleflag("max_hair_color");
	.list$[Job_Summoner + LOOK_CLOTHES_COLOR] = getbattleflag("min_cloth_color") +"-1";

	freeloop true;
	setarray .look[0], LOOK_HAIR, LOOK_HAIR_COLOR, LOOK_CLOTHES_COLOR, LOOK_BODY2,
		Job_Summoner + LOOK_HAIR, Job_Summoner + LOOK_HAIR_COLOR, Job_Summoner + LOOK_CLOTHES_COLOR;
	for ( .@i = 0; .@i < 7; ++.@i ) {
		deletearray .@array$;
		.@index = 0;
		explode( .@array$, .list$[.look[.@i]], "," );
		.@size = getarraysize(.@array$);
		for ( .@j = 0; .@j < .@size; ++.@j ) {
			if ( compare( .@array$[.@j], "-" ) ) {
				explode( .@range$, .@array$[.@j], "-" );
				.@min = atoi(.@range$[0]);
				.@max = atoi(.@range$[1]);
				.@range = .@max - .@min +1;
				for ( .@k = 0; .@k < .@range; ++.@k )
					setd ".list"+ .look[.@i] +"["+( .@index++ )+"]", .@min + .@k;
			}
			else
				setd ".list"+ .look[.@i] +"["+( .@index++ )+"]", atoi( .@array$[.@j] );
		}
		.maxindex[.look[.@i]] = .@index -1;
		.minstyles[.look[.@i]] = getd(".list"+ .look[.@i]);
		.maxstyles[.look[.@i]] = getd(".list"+ .look[.@i] +"["+( .@index -1 )+"]");
	}
	end;
}
