// https://annieruru.blogspot.com/2019/01/mapmoblist-script-version.html

// pre-re/mobs/fields/prontera.txt
//==================================================
// prt_fild07 - Prontera Field 
//==================================================
prt_fild07,0,0,0,0	monster	Rocker	1052,80,0,0,0
prt_fild07,0,0,0,0	monster	Poporing	1031,30,0,0,0
prt_fild07,0,0,0,0	monster	Vocal	1088,1,1800000,1200000,0
prt_fild07,225,110,5,5	monster	Black Mushroom	1084,3,360000,180000,1

-	script	sdjflsdf	-1,{
OnInit:
	bindatcmd "mapmoblist2", strnpcinfo(0)+"::Onaaa";
	end;
Onaaa:
	if ( .@atcmd_numparameters ) {
		if ( getmapusers(.@atcmd_parameters$) == -1 ) {
			dispbottom "Map not exist";
			end;
		}
		.@map$ = .@atcmd_parameters$;
	}
	else
		.@map$ = strcharinfo(3);
	.@size = getunits( BL_MOB, .@bl, false, .@map$ );
	freeloop true;
	for ( .@i = 0; .@i < .@size; ++.@i ) {
		.@string$ = rid2name(.@bl[.@i]);
		if ( !getd(".@"+ .@string$) ) {
			.@mobname$[.@c] = .@string$;
			getmapxy .@map$, .@x, .@y, UNITTYPE_MOB, .@bl[.@i];
			.@mobx[.@c] = .@x;
			.@moby[.@c] = .@y;
			.@mobhp[.@c] = getunitdata( .@bl[.@i], UDT_HP );
			.@mobmaxhp[.@c] = getunitdata( .@bl[.@i], UDT_MAXHP );
			++.@c;
		}
		setd ".@"+ .@string$, getd(".@"+ .@string$) +1;
	}
	dispbottom " === These are monsters in "+ .@map$ +" ===";
	for ( .@i = 0; .@i < .@c; ++.@i )
		if ( getd(".@"+ .@mobname$[.@i]) )
			dispbottom ( ++.@j )+". "+ .@mobname$[.@i] +" : "+ getd(".@"+ .@mobname$[.@i])+"x";
	if ( getgmlevel() < 99 ) end; // Hercules doesn't catch dead monsters as being alive
	dispbottom " === Location of unique monsters === ";
	for ( .@i = 0; .@i < .@c; ++.@i )
		if ( getd(".@"+ .@mobname$[.@i]) == 1 )
			dispbottom .@mobname$[.@i] +" --> X = "+ .@mobx[.@i] +" Y = "+ .@moby[.@i] +" | Hp left = "+ .@mobhp[.@i] +"/"+ .@mobmaxhp[.@i];
	end;
}