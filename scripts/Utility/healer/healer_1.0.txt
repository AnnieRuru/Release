//===== Hercules Script ======================================
//= Healer npc
//===== By: ==================================================
//= AnnieRuru
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= Hercules 2015-12-02
//===== Description: =========================================
//= my custom healer script used on defunt ComelRO
//===== Topic ================================================
//= http://herc.ws/board/topic/11193-edite-my-healer-by-annieruru/?hl=healer#entry66007
//===== Additional Comments: =================================  
//= ComelRO was a high rate server ... I posted it on eathena forum when it went down
//= https://www.eathena.ws/board/index.php?s=&showtopic=274421&view=findpost&p=1504415
//============================================================

prontera,155,185,5	script	Healer	1_F_MARIA,{

	.@cooldown = 10; // how many seconds before the player can heal again

	if ( @heal_cooldown + .@cooldown > gettimetick(2) ) {
		dispbottom "You need wait for " + ( @heal_cooldown + .@cooldown - gettimetick(2) ) + " seconds.";
		end;
	}

//	disable negative buffs ... I out from RO scene for about 4 years, so this is my only known negative buffs
	sc_end SC_STONE;
	sc_end SC_FREEZE;
	sc_end SC_STUN;
	sc_end SC_SLEEP;
	sc_end SC_POISON;
	sc_end SC_CURSE;
	sc_end SC_SILENCE;
	sc_end SC_CONFUSION;
	sc_end SC_BLIND;
	sc_end SC_BLOODING;
	sc_end SC_DPOISON;
	sc_end SC_FEAR;
	sc_end SC_COLD;
	sc_end SC_BURNING;
	sc_end SC_DEEP_SLEEP;
	sc_end SC_DEC_AGI;
	sc_end SC_BROKENARMOR;
	sc_end SC_BROKENWEAPON;
	sc_end SC_NOEQUIPWEAPON;
	sc_end SC_NOEQUIPSHIELD;
	sc_end SC_NOEQUIPARMOR;
	sc_end SC_NOEQUIPHELM;
	sc_end SC__STRIPACCESSARY;
	sc_end SC_PROPERTYUNDEAD;
	sc_end SC_ORCISH;
	sc_end SC_BERSERK;
	sc_end SC_SKE;
	sc_end SC_SWOO;
	sc_end SC_SKA;

//	give players buff ... don't give too much otherwise player wanna spam
	.@time = 300000; // the buff last 5 minutes
	sc_start SC_INC_AGI, .@time, 10;
	sc_start SC_BLESSING, .@time, 10;
//	sc_start SC_CONCENTRATION, .@time, 10;
//	sc_start SC_IMPOSITIO, .@time, 5;
//	sc_start SC_SUFFRAGIUM, .@time, 3;
//	sc_start SC_KYRIE, .@time, 10;
//	sc_start SC_MAGNIFICAT, .@time, 5;
//	sc_start SC_GLORIA, .@time, 5;
//	sc_start SC_WINDWALK, .@time, 5;
//	sc_start SC_ANGELUS, .@time, 10;

//	give player's soul link
	if ( .@spirit = .spirit[BaseJob] );
	else if ( Upper & 1 && BaseLevel < 70 )
		.@spirit = SL_HIGH;
	if ( .@spirit ) {
		sc_start4 SC_SOULLINK, .@time, 1, .@spirit,0,0;
		skilleffect .@spirit, 1;
	}

//	automatically turn all oridecon and elunium stone into pure stone to reduce player's weight
	.@ori = countitem(Oridecon_Stone);
	if ( .@ori >= 5 ) {
		delitem Oridecon_Stone, .@ori / 5 * 5;
		getitem Oridecon, .@ori / 5;
		message strcharinfo(0), "Refined "+ ( .@ori / 5 * 5 ) +" Oridecon Stones, got "+ ( .@ori / 5 ) +" Oridecons.";
	}
	.@elu = countitem(Elunium_Stone);
	if ( .@elu >= 5 ) {
		delitem Elunium_Stone, .@elu / 5 * 5;
		getitem Elunium, .@elu / 5;
		message strcharinfo(0), "Refined "+ ( .@elu / 5 * 5 ) +" Elunium Stones, got "+ ( .@elu / 5 ) +" Eluniums.";
	}

//	repair all player's broken eqiupments
	repairall;

//	identify all player's equipments
	getinventorylist;
	for( .@i = 0; .@i < @inventorylist_count; .@i++ ) {
		if ( @inventorylist_identify[.@i] == 1 )
			continue;
		else if ( getskilllv(MC_IDENTIFY) == 1 && Sp >= 10 )
			heal 0,0; // lol... this is supposed to reduce their SP by 10, whatever
		else if ( countitem(Spectacles) )
			delitem Spectacles,1;
		else if ( getskilllv(RG_COMPULSION) && Zeny >= ( 100 - ( 5 + 4 * getskilllv(RG_COMPULSION) ) )* 2/5 )
			Zeny -= ( 100 - ( 5 + 4 * getskilllv(RG_COMPULSION) ) )* 2/5;
		else if ( getskilllv(MC_DISCOUNT) && Zeny >= ( 100 - ( 5 + 2 * getskilllv(MC_DISCOUNT) ) )* 2/5 )
			Zeny -= ( 100 - ( 5 + 2 * getskilllv(MC_DISCOUNT) ) )* 2/5;
		else if ( Zeny >= 40 )
			Zeny -= 40;
		else
			break;
		delitem2 @inventorylist_id[.@i],1,0,0,0,0,0,0,0;
		getitem @inventorylist_id[.@i],1;
		.@icount++;
	}
	if ( .@icount )
		message strcharinfo(0), "Identified "+ .@icount +" Items.";

//	and last, give players heal
	percentheal 100,100;
	skilleffect AL_HEAL, MaxHp;
	skilleffect MG_SRECOVERY, MaxSp;

	emotion e_lv;
	@heal_cooldown = gettimetick(2);
	end;

OnInit:
	.spirit[Job_Alchemist] = SL_ALCHEMIST;
	.spirit[Job_Monk] = SL_MONK;
	.spirit[Job_Star_Gladiator] = SL_STAR;
	.spirit[Job_Sage] = SL_SAGE;
	.spirit[Job_Crusader] = SL_CRUSADER;
	.spirit[Job_SuperNovice] = SL_SUPERNOVICE;
	.spirit[Job_Knight] = SL_KNIGHT;
	.spirit[Job_Wizard] = SL_WIZARD;
	.spirit[Job_Priest] = SL_PRIEST;
	.spirit[Job_Bard] = .spirit[Job_Dancer] = SL_BARDDANCER;
	.spirit[Job_Rogue] = SL_ROGUE;
	.spirit[Job_Assassin] = SL_ASSASIN;
	.spirit[Job_Blacksmith] = SL_BLACKSMITH;
	.spirit[Job_Hunter] = SL_HUNTER;
	.spirit[Job_Soul_Linker] = SL_SOULLINKER;
	end;
}
